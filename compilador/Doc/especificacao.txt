/*
	Projeto de Compiladores
	
	autors: Thiago Felipe Miranda Arcanjo,
			Victor Ferreira Pereira
	versao: 1.0.0
	descricao: Desenvolver um compilador para a linguagem Portugol Simplificada
	
*/

Package compilador;

Helpers

	all 			= [0 .. 0xffff];
	letter 			= ['a' .. 'z'] | ['A' .. 'Z'];
	init_var 		= letter | '_';
	cr 				= 13;
	lf 				= 10;
	tab 			= 9;
	eol 			= cr lf | cr | lf;
	digit 			= ['0' .. '9'];
	blank 			= ' ' | cr | lf | tab;
	char			= [all - '''];
	comment_line	= '/''/'[all - [lf + cr]]* eol;
	
States 
	
	normal, comment;
	
Tokens

		{normal} programa 		= 'programa';
		{normal} inicio 		= 'inicio';
		{normal} fim 			= 'fim.';
		{normal} enquanto 		= 'enquanto';
		{normal} fim_enquanto	= 'fim enquanto';
		{normal} se 			= 'se';
		{normal} entao 			= 'entao';
		{normal} senao 			= 'senao';
		{normal} fim_se 		= 'fim se';
		{normal} para 			= 'para';
		{normal} de				= 'de';
		{normal} passo			= 'passo';
		{normal} fim_para 		= 'fim para';
		{normal} ate 			= 'ate';
		{normal} faca 			= 'faca';
		{normal} repita 		= 'repita';
		{normal} avalie			= 'avalie';
		{normal} caso 			= 'caso';
		{normal} fim_avalie 	= 'fim avalie';
		{normal} tipo 			= 'inteiro' | 'real' | 'caracter';
		
		{normal} mais 			= '+';
		{normal} menos 			= '-';
		{normal} barra 			= '/';
		{normal} estrela 		= '*';
		{normal} par_d 			= '(';
		{normal} par_e 			= ')';
		{normal} col_d			= '[';
		{normal} col_e			= ']';
		{normal} igual			= '=';
		{normal} diferente		= '<>';
		{normal} maior			= '>';
		{normal} menor			= '<';
		{normal} menor_igual	= '<=';
		{normal} maior_igual	= '>=';
		{normal} e				= 'e';
		{normal} ou				= 'ou';
		{normal} xor			= 'xor';
		{normal} nao			= 'nao';
		{normal} atribuicao		= ':=';
		
		{normal} escreva		= 'escreva';
		{normal} leia			= 'leia';
		
		{normal} dois_pontos 	= ':';
		{normal} semicolon	 	= ';';
		{normal} ponto 			= '.';
		{normal} virgula		= ',';
		
		{normal} comments		= comment_line;
		
		{normal} id 			= init_var (letter | digit | '_')*;
		{normal} number		 	= '-'? (digit+);
		{normal} number_real 	= '-'? digit+ '.' digit+;
		{normal} string			= ''' char* ''';
		{normal} blank 			= blank+;
	
		{normal->comment, comment} comment = '/*';

		{comment} comment_end = '*/'; 
		{comment} comment_body = [all - ['*' + '/']]*; 
		{comment} star = '*'; 
		{comment} slash = '/';
		{comment} comment_line		= comment_line;
	
Ignored Tokens

	blank, comments, comment;

Productions

	init				= start?;
	start 				= programa id inicio declaracao? comandos fim;
	declaracao			= declaracao? tipo dois_pontos var semicolon;
	
	var					= {id} identificador var_tail*;
	
	var_tail			= virgula identificador;
	
	exp					= {exp} expressao exp_tail*;
	
	exp_tail			=  virgula expressao;
						
	identificador		= {id} id 
						| {id_var} id col_d number col_e;
	
	valor				= {string} string
						| {number} number
						| {number_real} number_real;
	
	comandos			= comando*;
	
	comando				= {atribuicao}  identificador atribuicao expressao semicolon
						| {leia}  leia par_d var par_e semicolon
						| {escreva}  escreva par_d exp par_e semicolon
						| {se} se_stt
						| {enquanto} enquanto par_d expressao_logica par_e faca comandos fim_enquanto semicolon
						| {avalie} avalie par_d expressao_logica par_e caso_stt* senao_caso_stt? fim_avalie semicolon
						| {repita} repita comandos ate par_d expressao_logica par_e semicolon
						| {para} para identificador de [inicial]:number ate [final]:number faca comandos fim_para semicolon
						| {para_passo} para identificador de [inicial]:number passo [passos]:number ate [final]:number faca comandos fim_para semicolon;
		
	se_stt				= se expressao_logica entao comandos senao_stt? fim_se semicolon;
	
	senao_stt			= senao comandos;
	
	caso_stt			= caso valor dois_pontos comandos;
	senao_caso_stt		= senao dois_pontos comandos;
	
	expressao			= {mais} termo mais expressao
						| {menos} termo menos expressao
						| {negacao} menos expressao
						| {termo} termo;
						
	termo				= {mul} fator estrela termo
						| {div} fator barra termo
						| {fator} fator;
						
	fator				= {valor} valor
						| {id} identificador
						| {parenteses} par_d expressao par_e;

	expressao_logica	= {logico} termo_logico op_logico expressao_logica
						| {negacao} nao expressao_logica
						| {termo} termo_logico;
					
	termo_logico		= {relacional} expressao op_rel termo_logico
						| {fator} fator_logico;
					
	fator_logico		= {expressao} par_d expressao_logica par_e
						| {base_relacional} [esq]:expressao op_rel [dir]:expressao;
	
	op_rel				= {igual} igual
						| {diferente} diferente
						| {maior} maior
						| {menor} menor
						| {maior_igual} maior_igual
						| {menor_igual} menor_igual;
						
	op_logico			= {e} e
						| {ou} ou
						| {xor} xor;